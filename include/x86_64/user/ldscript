/* Simple linker script for ucore user-level programs.
   See the GNU ld 'info' manual ("info ld") to learn the syntax. */

OUTPUT_FORMAT("elf64-x86-64", "elf64-x86-64", "elf64-x86-64")
OUTPUT_ARCH(i386:x86-64)
ENTRY(__init_entry)

SECTIONS {
    /* Load programs at this address: "." means the current address */
    . = 0x0000000040000000;

	PROVIDE(__bin_start = .);

    .init : {
        *init.S.o*(.text .rodata .data)
	}

    .text : {
        *(.text .stub .text.* .gnu.linkonce.t.*)
    }

	. = ALIGN(0x1000);

    .rodata : {
        *(.rodata .rodata.* .gnu.linkonce.r.*)
    }

    /* Adjust the address for the data segment to the next page */
    . = ALIGN(0x1000);

    .data : {
        *(.data)
    }

	. = ALIGN(0x1000);

    PROVIDE(__bin_edata = .);

    .bss : {
        *(.bss)
    }

	. = ALIGN(0x1000);
    PROVIDE(__bin_end = .);

    /DISCARD/ : {
        *(.eh_frame .note.GNU-stack .comment)
    }
}
